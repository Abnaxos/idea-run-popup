import java.time.LocalDateTime
import org.ajoberstar.grgit.Grgit


buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.3.6'
        classpath 'org.ajoberstar:grgit:1.7.2'
    }
}

buildDir = 'target'
apply plugin:'java'
apply plugin:'idea'
apply plugin:'org.jetbrains.intellij'

intellij {
    version = 'IC-2018.3'
    updateSinceUntilBuild = false
}

Grgit git
try {
//noinspection GroovyAssignabilityCheck
    git = Grgit.open(dir: project.rootDir)
} catch (e) {
    throw new ProjectConfigurationException("Cannot open git repository: $e", e)
}
String currentBranch = git.branch.current.name

//noinspection GroovyAssignabilityCheck
if (currentBranch == 'master') {
    List<String> candidates = git.tag.list()*.name?.findAll({it =~ /\d+(\.\d+)+/})
    if (candidates.size() != 1) {
        throw new ProjectConfigurationException("No distinct version tag found; candidates: $candidates", null)
    }
    version = candidates[0]
} else {
    //noinspection GroovyAssignabilityCheck
    version = (~/.*\/(\d+(\.\d+)+)/).matcher(currentBranch).with {
        matches() ? "${group(1)}-SNAPSHOT" : 'SNAPSHOT'
    }
}
logger.quiet "Version from git: $project.version"
project.group = 'ch.raffael.idea.plugins.runpopup'

//noinspection GroovyAssignabilityCheck
task updateLicense() {

    ext.copyrightProfile = file(file('.idea/copyright/MIT.xml'))
    ext.licenseFile = file('LICENSE')

    inputs.file copyrightProfile
    outputs.file licenseFile

    doFirst {
        String lic = new XmlSlurper().parse(copyrightProfile as File).copyright.option.find({it.@name=='notice'}).@value as String
        lic = lic.replaceAll('(\\\$|&#36;)today.year(\\b)', "${LocalDateTime.now().year}\$2")
        licenseFile.text = lic
    }

}

buildPlugin {
    dependsOn updateLicense
    from updateLicense.licenseFile
}

patchPluginXml {

    String repoContentRootUrl = 'https://raw.githubusercontent.com/Abnaxos/idea-run-popup/'
    if (currentBranch == 'master') {
        repoContentRootUrl += project.version
    } else {
        repoContentRootUrl += 'develop'
    }

    inputs.property 'repoContentRootUrl', repoContentRootUrl

    doLast {
        new File(destinationDir, 'plugin.xml').with {
            setText(getText('UTF-8').replace('@@repoContentRootUrl@@', repoContentRootUrl), 'UTF-8')
        }
    }
}
