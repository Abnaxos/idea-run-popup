import java.time.LocalDateTime


buildDir = 'target'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.3.6"
    }
}

apply plugin:'java'
apply plugin:'idea'
apply plugin:'org.jetbrains.intellij'

version = '1.0-SNAPSHOT' //#prepare-release
group = 'ch.raffael.idea.plugins.runpopup'

intellij {
    version = 'IC-2018.2.1'
}

//noinspection GroovyAssignabilityCheck
task updateLicense() {

    ext.copyrightProfile = file(file('.idea/copyright/MIT.xml'))
    ext.licenseFile = file('LICENSE')

    inputs.file copyrightProfile
    outputs.file licenseFile

    doFirst {
        String lic = new XmlSlurper().parse(copyrightProfile).copyright.option.find({it.@name=='notice'}).@value as String
        lic = lic.replaceAll('(\\\$|&#36;)today.year(\\b)', "${LocalDateTime.now().year}\$2")
        licenseFile.text = lic
    }

}

buildPlugin {
    dependsOn updateLicense
    from updateLicense.licenseFile
}

//noinspection GroovyAssignabilityCheck
task prepareRelease() {
    final charset = 'UTF-8'
    doFirst {
        file('build.gradle').with { f ->
            logger.quiet "Updating: $f"

            f.write(
                    f.getText(charset).
                            replaceAll('(?m)^[ \\t]*version\\s*=.*\\D(\\d[-.\\d]+)-SNAPSHOT.*//#prepare-release[ \\t]*$', 'version = \'$1\''),
                    charset)
        }
        file('src/main/resources/META-INF/plugin.xml').with { f ->
            logger.quiet "Updating: $f"
            f.write(
                    f.getText(charset).
                            replaceAll('(?m)(.*)/develop/images/(.*)<!--#prepare-release-->[ \\t]*$', '$1/master/images/$2'),
                    charset)
        }
    }
}
